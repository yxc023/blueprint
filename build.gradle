// 构建的前置脚本, 用于添加构建脚本的所需依赖
buildscript {
    // 设置变量参数
    ext {
        springBootVersion = '2.1.0.RELEASE'
    }
    // 声明依赖所在仓库
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public"
        }
    }
    // 声明依赖
    dependencies {
        classpath("org.springframework.boot:spring-boot-gradle-plugin:${springBootVersion}")
        classpath 'org.asciidoctor:asciidoclet:1.+'
    }
}

// 对每个子工程设置
subprojects { p ->
    apply plugin: 'java'

    // 设置 groupid 和 version, artifact id 由子项目文件夹名字决定
    group = "com.yangxiaochen.blueprint"
    version = "1.0.0-SNAPSHOT"
    sourceCompatibility = 1.8

    // 设置项目依赖用的仓库
    repositories {
        maven {
            url "http://maven.aliyun.com/nexus/content/groups/public"
        }
    }

    configurations {
        asciidoclet
    }

    // 设置通用依赖
    dependencies {
        asciidoclet 'org.asciidoctor:asciidoclet:1.+'
        testImplementation 'junit:junit:4.12'
        implementation 'org.slf4j:slf4j-api:1.7.25'
        implementation 'org.projectlombok:lombok:1.18.2'
        // 注解处理器
        annotationProcessor 'org.projectlombok:lombok:1.18.2'
    }




    tasks.withType(JavaCompile) {
        options.encoding = 'UTF-8'
    }

    tasks.withType(Javadoc) {
        options.docletpath = configurations.asciidoclet.files.asType(List)
        options.doclet = 'org.asciidoctor.Asciidoclet'
        options.addStringOption "-base-dir", "${p.projectDir}"
        failOnError false
        options.encoding = 'UTF-8'
        options.charSet = 'UTF-8'
        options.docEncoding = 'UTF-8'
    }
}